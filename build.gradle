apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'codenarc'

defaultTasks 'clean', 'libs'
sourceSets {
    jobs {
        groovy {
            srcDir 'sample/jobs'
            srcDir 'platform/jobs'
            srcDir 'testeng/jobs'
            srcDir 'mobileApp/jobs'
        }
    }
    views {
        groovy {
            srcDir 'platform/views'
        }
    }
    src {
        groovy {
            srcDir 'src/main/groovy'
        }
    }
    test {
        groovy {
            srcDir 'src/test/groovy'
        }
    }
}

repositories {
    jcenter() {
        artifactUrls 'https://repo.jenkins-ci.org/public/'
    }
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
    mavenCentral()
}

configurations {
    libs
}

dependencies {

    libs 'org.yaml:snakeyaml:1.17'
    
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.codehaus.groovy:groovy:2.4.11'
    testCompile 'org.codehaus.groovy:groovy:2.4.11'
    compile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    jobsCompile 'org.yaml:snakeyaml:1.17'
    jobsCompile 'org.codehaus.groovy:groovy:2.1.3'
    jobsCompile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }
    // Needed for mocking in tests
    testCompile 'cglib:cglib:3.1' 

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.6@jar'
    testCompile 'org.jenkins-ci.plugins:structs:1.6'

    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:4.4@jar'
    testCompile 'com.cloudbees.plugins:build-flow-plugin:0.17@jar'
    testCompile 'org.jvnet.hudson.plugins:hipchat:0.1.9@jar'
}

codenarc {
    toolVersion = '0.25.2'
    configFile = file('config/codenarc/codenarcRules.groovy')
    maxPriority2Violations = 75
    maxPriority3Violations = 99

    // Display codenarc violations in the console for travis builds
    // otherwise, default to creating an html report
    if (System.getenv('CI_SYSTEM') == 'travis') {
        reportFormat = 'console'
    }
}

task libs(type: Copy) {
    into 'lib'
    from configurations.libs
}

tasks.getByName('test').inputs.files(sourceSets.jobs.groovy.srcDirs)

test {
    inputs.files sourceSets.jobs.groovy.srcDirs
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
